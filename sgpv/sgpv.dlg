/*
To-Do/Further improvements: 	Add additional input checks for nullweights and altweights to allow only certain inputs for nullspace.
								Add another tab to launch the other dialogs for the other commands to make SGPV the central dialog.
								Add input checks for the options nullhi and nulllo together with the coefficient-option to support better multiple null-hypotheses.
*/
/*
sgpv dialog version 1.04, 21.05.2020, Sven-Kristjan Bormann
for sgpv version 1.1

To install in User Statistics menu via Stata commands:
  
  . window menu append item "stUserStatistics" "SGPV (Main Command) (&sgpv)" "db sgpv"

  . window menu refresh

To permanently install, place the commands in your -profile.do- file.


*/

VERSION 12.0
POSITION . . 420 400
INCLUDE _std_xlarge
//INCLUDE _ht320
//INCLUDE header
OK ok1,      label("OK")
CANCEL can1, label("Cancel")
SUBMIT sub1, label("Submit") 
HELP hlp1,   view("help sgpv")  
RESET res1
COPY copy1


//Copied from format.dlg
SCRIPT PREINIT
BEGIN
        create STRING formatString
        setscale_hadj resource FORMAT_DLG_HSCALE
END


LIST nullweights_list
BEGIN
	Point
	Uniform
	TruncNormal
END

LIST altweights_list
BEGIN
	Uniform
	TruncNormal
END

LIST bonus_values
BEGIN
	none // a filler option introduced to have a default option for the dropdownlist
	deltagap
	fdrisk
	all
END

LIST bonus_list
BEGIN
	None
	Delta-Gap
	False discovery risk
	Both
END


LIST bonus_action
BEGIN
	script fdrisk_off
	script fdrisk_off
	script fdrisk_on
	script fdrisk_on	 
	  

END

LIST inttype_list
BEGIN
	confidence
	likelihood
END


DIALOG main, label("sgpv 1.1 - Second Generation P-Values") tabtitle("Main")
BEGIN

	GROUPBOX gp_calcfor 		5 	5 		410 	110, label("Calculate SGPVs for:")
	RADIO 	r1 				_lft	20		50 		., first label("Replay") onclickon(script replay_on) ///
													tooltip("Replay the previous estimation command and calculate the SGPVs and bonus statistics for it.")
	RADIO 	r2 				+70		@ 	 	150 	., 	middle label("Stored Estimate") onclickon(script estimate_on) ///
														tooltip("Calculate SGPVs for a previously stored estimate.")
	COMBOBOX cb_estimate 	80	 	_ss	 	150		., dropdown content(estimates) option(estimate)

	RADIO 	r3 				+180 	20	 	@ 		., middle label("Matrix") onclickon(script matrix_on)
	COMBOBOX cb_matrix	 	@ 		_ss 	150	 	., dropdown content(matrix) option(matrix) ///
														tooltip("The selected needs to have the same layout as the matrix r(table) which is returned by Stata estimation commands. See the help file for more information.")
	RADIO 	r4  			10 		_ls 	150		 ., last label("Estimation command") onclickon(script cmd_on) ///
														tooltip("Enter the estimation command for which you want to calculate the SGPVs and bonus statistcs.")
	EDIT ed_cmd 			@ 		_ss 	400 	., label(resource LBL_STATA_COMMAND_TO_RUN) 
	
	CHECKBOX ck_coefficient 10  	_ls 	400 	., label(resource ESTIMATES_TABLE_OPT_CK_KEEP) ///
														clickon(main.vl_coefficient.enable) clickoff(main.vl_coefficient.disable)
	VARLIST vl_coefficient 	@ 		_ss 	@ 		., option(coefficient) numeric ///
														tooltip("The variable name needs to be exactly the same as returned by estimation output in the matrix r(table). If you did not use factor-variable notation, then the names are identical to the variable names. Otherwise you have to use the factor-variable notation e.g. 1.foreign if you estimated 'reg price mpg i.foreign'.")
	TEXT tx_coefficient 	@ 		_ss		@ 		., label(resource ESTIMATES_TABLE_OPT_TX_KEEP)
	TEXT tx_coefficient2 	@ 		_vss 	@ 		., label(resource ESTIMATES_TABLE_OPT_TX_KEEP2)
	TEXT tx_coefficient3    @		_ss		@		., label("Only one of these specifications can be used at the same time.")
	CHECKBOX ck_noconstant  @		_ls 	@		., label(resource LBL_NOCONSTANT) option(noconstant) tooltip("Do not calculate SGPVs, delta-gaps and Fdrs for the constant term.")
	//TEXT tx_coefficient3	@		_vss	@		., label("The variable name needs to be exactly the same as returned by estimation output in the matrix r(table). If you did not use factor-variable notation, then the names are identical to the variable names.  For example: 1.foreign ")
	
	GROUPBOX gb_null 		5 		_ls 	410 	140, label("Null-Hypothesis Interval(s)")
	TEXT tx_nulllo 			15 		_ss 	170 	., label("Lower Bound(s)") 
	EDIT ed_nulllo 			@ 		_ss		 170 	 ., option(nulllo) default(0) tooltip("The default value 0 is just meant to be used for an easier beginning when starting to use SGPVs. Please change this value to something more reasonable. Using this default value will always result in having SGPVs of value 0 or 0.5!")
	//Add here infos about multiple null-hypotheses
	
	TEXT tx_nullhi 			+220 	-20 	170 	., label("Upper Bound(s)") 
	EDIT ed_nullhi 			@ 		_ss 	170 	., option(nullhi) default(0) tooltip("The default value 0 is just meant to be used for an easier beginning when starting to use SGPVs. Please change this value to something more reasonable. Using this default value will always result in having SGPVs of value 0 or 0.5!")
	
	TEXT tx_null1			15		_ls		390		., label("To have an individual null-hypothesis for each coefficient,")
	TEXT tx_null2			@		_vss	@		., label("you need to separate the bounds with spaces.")
	TEXT tx_null3			@		_vss	@ 		., label("The number of bounds need to match the number of coefficients.")

END

DIALOG further, tabtitle("Further options")
BEGIN
	GROUPBOX gb_display		_top	_lft 		405		180,label("Display options")
	CHECKBOX ck_quiet 		+10 	+20 		100 	., label("Quietly") option(quiet) tooltip("Do not display estimation results.")
	CHECKBOX ck_nonull		+120		@    		200		., label("No 0 Point Null-Hypothesis warnings") option(nonullwarnings) tooltip("Deactive the warning messages shown when using the default point 0 null-hypothesis.")
	
	TEXT tx_matlist 		15 		_ss 	200 	., label("Further results display options")
	EDIT ed_matlist 		@ 		_ss 	320 	., option(matlistopt) tooltip("Change the format of the displayed matrix. The same options as for the command matlist can be used.")
	BUTTON bu_matlist 		+330	@ 		60 		., label("Help") onpush("view help matlist") 
			
	TEXT tx_bonus 			15 		_ss 	150 	., label("Bonus Statistics:")
	COMBOBOX cb_bonus 		@ 		_ss 	180 	., dropdownlist content(bonus_list) values(bonus_values) option(bonus) default(none)  ///
														tooltip(Display and calculate the bonus statistics delta-gap and fdr/fcr. If `"None"' is selected then no bonus statistcs are calculated and displayed.) ///
														onselchangelist(bonus_action) 
	TEXT     tx_format    	15    	+25     200    .,   label(resource LBL_FORMAT) 
	EDIT     ed_format    	@       _ss     320   .,    label(resource LBL_FORMAT) option(format) default(%5.4f) ///
													 tooltip("Specify the format for displaying the individual elements of the result matrix.   This option is NOT identical to the option above, but works independently of it. Setting the format option via the option above overrides the setting here and also changes the format of the column names.")
	BUTTON   bu_format    350  @       60      .,    label(resource LBL_BU_CREATE) onpush(script showFormatDialog) tooltip(resource LBL_CREATE_DISPLAY_FORMAT)
	
	GROUPBOX gb_fdrisk 	 _top	200	405 	190, label("Fdrisk options")
	TEXT tx_nullspace      15   +10    200    ., label("Null hypothesis parameter space:")
	EXP ex_nullspace       @    _ss    400    ., option(nullspace) error(Nullspace) label("Expression") tooltip("Support of the null probability distribution.")
	
	TEXT tx_nullweights     15    _ls    150    ., label("Null hypothesis distribution:")
	COMBOBOX cb_nullweights @    _ss     100    ., dropdownlist content(nullweights_list) option(nullweights) error(Nullweights) default(Point) ///
													tooltip("The probability distribution for the null parameter space.")
	
	TEXT tx_altweights      15   _ls      170    ., label("Alternative hypothesis distribution:")
	COMBOBOX cb_altweights  @   _ss      100    ., dropdownlist content(altweights_list) option(altweights) error(Altweights) default(Uniform) ///
												tooltip("Probability distribution for the alternative parameter space")
	
	TEXT tx_inttpye       	180  -70      @    ., label("Interval type:")
	COMBOBOX cb_inttype     @  _ss      @    ., dropdownlist content(inttype_list) option(inttype) error(Interval type) default(confidence)
	
	TEXT tx_intlevel 		 @  _ls      @    ., label("Interval level:")
	EDIT ed_intlevel         @  _ss      @    ., option(intlevel) error(Interval level) default(0.05)
	
	TEXT tx_pi0           +120  -70     120   ., label("Prior probability of H0:")
	EDIT ed_pi0              @  _ss      50   ., option(pi0) default(0.5) numonly tooltip("This value can be only between 0 and 1 (exclusive).")

END

/*
DIALOG fdrisk, tabtitle("Fdrisk options")
BEGIN
	TEXT tx_nullspace      10    10    200    ., label("Null hypothesis parameter space:")
	EXP ex_nullspace       @    _ss    400    ., option(nullspace) error(Nullspace) label("Expression") tooltip("Support of the null probability distribution.")
	
	TEXT tx_nullweights     10    _ls    150    ., label("Null hypothesis distribution:")
	COMBOBOX cb_nullweights @    _ss     100    ., dropdownlist content(nullweights_list) option(nullweights) error(Nullweights) default(Point) ///
													tooltip("The probability distribution for the null parameter space.")
	
	//TEXT tx_altspace     	10   _ls      200    ., label("Alternative hypothesis parameter space:")
	//EXP ex_altspace        	@   _ss      400    ., option(altspace) error(Altspace) label("Expression") tooltip("Support for the alternative probability distribution")
	
	TEXT tx_altweights      10   _ls      200    ., label("Alternative hypothesis distribution:")
	COMBOBOX cb_altweights  @   _ss      100    ., dropdownlist content(altweights_list) option(altweights) error(Altweights) default(Uniform) ///
												tooltip("Probability distribution for the alternative parameter space")
	
	TEXT tx_inttpye       	@  _xxls      @    ., label("Interval type:")
	COMBOBOX cb_inttype     @  _ss      @    ., dropdownlist content(inttype_list) option(inttype) error(Interval type) default(confidence)
	
	TEXT tx_intlevel 		 @  _ls      @    ., label("Interval level:")
	EDIT ed_intlevel         @  _ss      @    ., option(intlevel) error(Interval level) default(0.05)
	
	TEXT tx_pi0           +150  -70     120   ., label("Prior probability of H0:")
	EDIT ed_pi0              @  _ss      50   ., option(pi0) default(0.5) numonly tooltip("This value can be only between 0 and 1 (exclusive).")

	
END
*/
//Copied from format.dlg
SCRIPT showFormatDialog 
BEGIN
        create CHILD format_chooser
        format_chooser.setExitString formatString
        format_chooser.setExitAction "further.ed_format.setvalue class formatString.value"
END


SCRIPT replay_on
BEGIN
	main.cb_estimate.disable
	main.cb_matrix.disable
	main.ed_cmd.disable
END

SCRIPT estimate_on
BEGIN
	main.cb_estimate.enable
	main.cb_matrix.disable
	main.ed_cmd.disable
END

SCRIPT matrix_on
BEGIN
	main.cb_estimate.disable
	main.cb_matrix.enable
	main.ed_cmd.disable
END

SCRIPT cmd_on
BEGIN
	main.cb_estimate.disable
	main.cb_matrix.disable
	main.ed_cmd.enable
END


//Deactive display of fdrisk options if bonus is set to none or deltagap 
SCRIPT fdrisk_off
BEGIN
	further.ex_nullspace.disable
	further.cb_altweights.disable
	further.cb_nullweights.disable
	further.cb_inttype.disable
	further.ed_intlevel.disable
	further.ed_pi0.disable
END

SCRIPT fdrisk_on
BEGIN
	further.ex_nullspace.enable
	further.cb_altweights.enable
	further.cb_nullweights.enable
	further.cb_inttype.enable
	further.ed_intlevel.enable
	further.ed_pi0.enable
END


PROGRAM sgpv_options
BEGIN
	put "sgpv "
	
	beginoptions		
		optionarg main.cb_matrix
		optionarg main.cb_estimate
		optionarg main.vl_coefficient
		option 	  main.ck_noconstant
		optionarg /hidedefault main.ed_nullhi
		optionarg /hidedefault main.ed_nulllo
		
		option further.ck_quiet
		option further.ck_nonull
		
		optionarg further.ed_matlist
		optionarg /hidedefault further.cb_bonus
		optionarg /hidedefault further.ed_format
		
		optionarg further.ex_nullspace
		optionarg /hidedefault further.cb_nullweights 
		*optionarg further.ex_altspace
		optionarg /hidedefault further.cb_altweights 
		optionarg /hidedefault further.cb_inttype 
		optionarg /hidedefault further.ed_intlevel
		optionarg /hidedefault further.ed_pi0
	endoptions
END

PROGRAM command // inspired by the structure used in bootstrap.dlg
BEGIN
	put /program sgpv_options
	
	if main.ed_cmd.isenabled(){
		put " " ": "
		put main.ed_cmd
	} 
END

